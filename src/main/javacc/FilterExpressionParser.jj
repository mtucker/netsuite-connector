options {
   STATIC = false ;
}
PARSER_BEGIN(FilterExpressionParser)
    package org.mule.module.netsuite.api.model.expression.filter;
    import com.netsuite.webservices.platform.core_2010_2.*;
    import com.netsuite.webservices.platform.core_2010_2.types.*;
    import  java.io.ByteArrayInputStream;

   /**
    * A predicate-like syntax parser for date expressions
    * @author flbulgarelli*/
   public class FilterExpressionParser {
   
     private FilterExpressionBuilder builder = new FilterExpressionBuilder(); 
   
     public static SearchRecord parse(SearchRecordType targetRecordType, String expression)  {
       org.apache.commons.lang.Validate.notEmpty(expression);
        try{
          return new FilterExpressionParser(new ByteArrayInputStream(expression.getBytes())).withTarget(targetRecordType).start();
      } catch(ParseException e){
         throw new IllegalArgumentException("Syntax error in date expression " + expression, e);
      }
    } 
    
    public FilterExpressionParser withTarget(SearchRecordType target) {
        builder.setTarget(target);
        return this;
    }
    
    private static String nullSafeImage(Token token) { 
      return token != null ? token.image : null;
    }
}
   
PARSER_END(FilterExpressionParser)


SKIP : { " " | "\n" | "\r" | "\t" }

TOKEN : { < COMMA : "," > }

TOKEN : { < DOT : "." > }

TOKEN : { < OPEN_PAR : "(" > }
TOKEN : { < CLOSE_PAR : ")" > }

TOKEN : { < UNQUOTED_STRING : (["a"-"z","A"-"Z","0"-"9"])+ > }

TOKEN : { < STRING : "'" (~["'"])* "'" | "\"" (~["\""])* "\"" > }

SearchRecord start() : 
{ }
{ 
   expression() ( < COMMA > expression() )* 
  {return builder.build();}
}

void expression() :
{Token operation, attributeOrJoin, attribute = null, arg0 = null, arg1 = null; }
{ operation = < UNQUOTED_STRING >
  < OPEN_PAR >
  attributeOrJoin = < UNQUOTED_STRING >
  (< DOT >
  attribute = < UNQUOTED_STRING >)? 
  (< COMMA >
  arg0 = < UNQUOTED_STRING >
  (< COMMA >
  arg1 = < UNQUOTED_STRING >)?)?
  < CLOSE_PAR > 
  { 
    if( attribute == null  ) 
      builder.addSimpleExpression(operation.image, attributeOrJoin.image, nullSafeImage(arg0), nullSafeImage(arg1));
    else
      builder.addJoinedExpression(operation.image, attributeOrJoin.image, attribute.image, nullSafeImage(arg0), nullSafeImage(arg1));
  }  
}


